plugins {
    id("org.jetbrains.kotlin.jvm") version "1.4.32"
    id("org.jetbrains.kotlin.kapt") version "1.4.32"
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id("io.micronaut.application") version "1.5.0"
    id("org.jetbrains.kotlin.plugin.allopen") version "1.4.32"
    id('pl.allegro.tech.build.axion-release') version '1.13.2'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url 'https://customer-wave-775846179576.d.codeartifact.us-east-1.amazonaws.com/maven/customer-wave/'
        credentials {
            username "aws"
            password System.env.CODEARTIFACT_AUTH_TOKEN
        }
    }
}

micronaut {
    runtime("lambda")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("micronaut.demo.*")
    }
}

dependencies {
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.aws:micronaut-aws-sdk-v2")
    implementation("io.micronaut.kotlin:micronaut-kotlin-runtime")
    implementation("javax.annotation:javax.annotation-api")
    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut.aws:micronaut-function-aws-api-proxy")
    implementation("io.micronaut.aws:micronaut-function-aws-custom-runtime")

    runtimeOnly("com.fasterxml.jackson.module:jackson-module-kotlin")
    runtimeOnly("ch.qos.logback:logback-classic")

    compileOnly("org.graalvm.nativeimage:svm")

    annotationProcessor("io.micronaut:micronaut-inject-java")
}


application {
    mainClass.set("micronaut.demo.StreamLambdaHandler")
}
nativeImage {
    args('--verbose')
    imageName('realwave-gender')
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
}

tasks {
    compileKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    task loadGitHubToken {
        scmVersion.repository.customUsername = gitUsername
        scmVersion.repository.customPassword = gitPassword
    }

    release {
        dependsOn loadGitHubToken
    }

    markNextVersion {
        dependsOn loadGitHubToken
    }
}

scmVersion {
    ignoreUncommittedChanges = false

    checks {
        aheadOfRemote = true
    }

    nextVersion {
        suffix = 'SNAPSHOT' // tag suffix
        separator = '-' // separator between version and suffix
    }

    hooks {
        pre 'fileUpdate', [file: 'gradle.properties', pattern: {v,p -> /version=$v-SNAPSHOT/}, replacement: {v, p -> "\$1$v $p"}]
        pre 'commit'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${group}"
            artifactId = "${artifactId}"
            version = "${version}"
            from components.java
        }
    }
    repositories {
        maven {
            url 'https://customer-wave-775846179576.d.codeartifact.us-east-1.amazonaws.com/maven/customer-wave/'
            credentials {
                username "aws"
                password System.env.CODEARTIFACT_AUTH_TOKEN
            }
        }
    }
}